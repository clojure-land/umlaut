<document> ::= (<SPACE*> statement)+

(* tokens *)
unicode-BOM ::= "\uFEFF"
white-space ::= #"\s+"
block-start ::= <"{">
block-end ::=  <"}">
group-start ::=  <"("> 
group-end ::= <")"> 
comment-char ::= "//"
comment ::= comment-char comment-value
<comment-value> ::= #".*\n"
SPACE ::= unicode-BOM | white-space | comment

(* general language features*)
<identifier> ::= #"[_A-Za-z][_0-9A-Za-z]*"
<value> ::= #"[\d\t\w <\(\[\{\\\^\-:\/,;'%$#@&#=\$!\|\]\}\?\*\+\.>]+"
<word-value> ::= #"[\d\t\w<\\\^\-:,;'\"%$#@&#=\$!\|\?\*\+\.>]+"

(* high-level section *)

<statement> ::= type | enum | interface | diagram | <SPACE+>

(* annotations *)
<annotation-space> ::= identifier <"/"> identifier
annotation ::= <"@"> annotation-space <SPACE+> identifier <SPACE+> word-value+ <",">? <SPACE+>?
documentation-annotation ::= <"@"> identifier <SPACE+> <"\""> value+ <"\""> <SPACE+>? <",">?
<annotation-block> ::= (annotation | documentation-annotation)
field-annotations ::= <"{"> <SPACE*> annotation-block+ <SPACE*> <"}">
<attribute-block> ::= <block-start> <SPACE*> annotation-block* <SPACE*> method+ <SPACE*> <block-end>

(* interface statement *)
interface ::= <"interface"> <SPACE+> identifier <SPACE*> attribute-block

(* class statement *)
type ::= <"type"> <SPACE+> identifier parent-set? <SPACE*> attribute-block

attribute ::= <SPACE*> identifier <SPACE*> <":"> <SPACE*> kind optional? <",">?
method ::= identifier <SPACE*> method-args? <SPACE*> <":"> <SPACE*> return-kind <SPACE*> field-annotations? <SPACE*>
<method-args> ::= <group-start> <SPACE*> attribute* <SPACE*> <group-end>
arity-value ::= #"[0-9]+" | "n"
<arity> ::= <"["> arity-value (<".."> arity-value) <"]">
optional ::= <"?">
return-kind ::= kind optional?
kind ::= identifier arity?
<parent-set> ::= <SPACE*> <":"> <SPACE*> parent*
parent ::= <SPACE*> identifier

(* enum statement *)
enum ::= <"enum"> <SPACE+> identifier <SPACE*> enum-block
<enum-block> ::= <block-start> <SPACE*> annotation-block* enum-entry+ <SPACE*> <block-end>
<enum-entry> ::= <SPACE+> identifier

(* diagram statement *)
diagram ::= <"diagram"> <SPACE+> identifier <SPACE*> diagram-block
<diagram-block> ::= <block-start> <SPACE*> diagram-group+ <SPACE*> <block-end>
diagram-group ::= <group-start> <SPACE*> diagram-entry+ <SPACE*> <group-end>"!"?
<diagram-entry> ::= <SPACE*> identifier

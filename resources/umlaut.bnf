<document> ::= statement*

(* general language features*)
spacer ::= ignore+
ignored ::= ignore*
ignore ::= unicode-BOM | white-space | new-line | comment
unicode-BOM ::= "\uFEFF"
white-space ::= #"[\x{9}\x{20}]"
block-start ::= <ignored> <"{">
block-end ::= <ignored> <"}">
group-start ::= <ignored> <"("> <ignored>
group-end ::= <ignored> <")"> <ignored>
new-line ::= #"\n"
comment-char ::= "//"
comment ::= comment-char comment-value
<identifier> ::= #"[_A-Za-z][_0-9A-Za-z]*"
<comment-value> ::= #".*\n"
<value> ::= #"[\d\t\w <\(\[\{\\\^\-:\/,;'%$#@&#=\$!\|\)\]\}\?\*\+\.>]+"
<word-value> ::= #"[\d\t\w<\\\^\-:,;'\"%$#@&#=\$!\|\?\*\+\.>]+"

(* high-level section *)
<statement> ::= <ignored> | type* | enum* | interface* | diagram*

(* annotations *)
<annotation-space> ::= identifier <"/"> identifier
annotation ::= <"@"> annotation-space <spacer> identifier <spacer> word-value+ <",">? <spacer>?
documentation-annotation ::= <"@"> identifier <spacer> <"\""> value+ <"\""> <spacer>? <",">?
field-annotations ::= <"{"> <ignored> (annotation|documentation-annotation)+ <ignored> <"}">

(* class statement *)
type ::= (annotation|documentation-annotation)* <"type"> <spacer> identifier parent-set? attribute-block
<attribute-block> ::= <block-start> <ignored> method+ <ignored> <block-end>
attribute ::= <ignored> identifier <ignored> <":"> <ignored> kind optional? <",">?
method ::= identifier <ignored> method-args? <ignored> <":"> <ignored> return-kind <ignored> field-annotations? <ignored>
<method-args> ::= <group-start> attribute* <group-end>
arity-value ::= #"[0-9]+" | "n"
<arity> ::= <"["> arity-value (<".."> arity-value) <"]">
optional ::= <"?">
return-kind ::= kind optional?
kind ::= identifier arity?
<parent-set> ::= <ignored> <":"> <ignored> parent*
parent ::= <ignored> identifier

(* interface statement *)
interface ::= (annotation|documentation-annotation)* <"interface"> <spacer> identifier attribute-block

(* enum statement *)
enum ::= (annotation|documentation-annotation)* <"enum"> <spacer> identifier enum-block
<enum-block> ::= <block-start> enum-entry+ <block-end>
<enum-entry> ::= <ignored> identifier

(* diagram statement *)
diagram ::= <"diagram"> <spacer> identifier diagram-block
<diagram-block> ::= <block-start> diagram-group+ <block-end>
diagram-group ::= <group-start> diagram-entry+ <group-end>"!"?
<diagram-entry> ::= <ignored> identifier